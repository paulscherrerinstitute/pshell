plugins {
    id "java"
    id "eclipse"
    id "maven"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.1"
}

version = '1.13.0' 
group = 'ch.psi'
ext.timestamp = new Date().format("yyMMddHHmm")

//py, groovy, js or all
ext.type = 'py'

ext.addopens = 'java.base/sun.nio.ch java.base/java.io java.desktop/sun.awt java.desktop/javax.swing.text.html java.desktop/sun.swing java.base/jdk.internal.loader'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
    ext.mainClass = 'ch.psi.pshell.ui.App' 
}

if (!hasProperty('runArgs')) {
    ext.arguments = ''
} else {
    ext.arguments = runArgs.split (" ")
} 


repositories {
    mavenCentral()
    jcenter()
    maven { url "http://maven.imagej.net/content/repositories/public/" }     
    maven { url "https://dl.bintray.com/paulscherrerinstitute/hltools/" }
}

configurations {
    provided
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

    
if (ext.type == 'all'){
    dependencies {
        compile 'org.python:jython-standalone:2.7.1'
        compile 'org.codehaus.groovy:groovy-all:2.4.8'
    }
} else if (ext.type == 'py'){
    dependencies {
        compile 'org.python:jython-standalone:2.7.1'
        provided 'org.codehaus.groovy:groovy-all:2.4.8'
    }    
} else if (ext.type == 'groovy'){    
    dependencies {
        provided 'org.python:jython-standalone:2.7.1'
        compile 'org.codehaus.groovy:groovy-all:2.4.8'
    }    
} else if (ext.type == 'js'){    
    dependencies {
        provided 'org.python:jython-standalone:2.7.1'
        provided 'org.codehaus.groovy:groovy-all:2.4.8'
    }
}

def urlFile = { url, name ->
    File file = new File("$buildDir/download/${name}.jar")
    file.parentFile.mkdirs()
    if (!file.exists()) {
        new URL(url).withInputStream { downloadStream ->
            file.withOutputStream { fileOut ->
                fileOut << downloadStream
            }
        }
    }
    files(file.absolutePath)
}

dependencies {
    compile 'ch.psi:jcae:2.9.7'   
    compile 'org.glassfish.jersey.media:jersey-media-json-jackson:2.27'
    compile 'org.glassfish.jersey.containers:jersey-container-grizzly2-http:2.27'
    compile 'org.glassfish.jersey.media:jersey-media-sse:2.27'
    compile 'org.glassfish.jersey.inject:jersey-hk2:2.27'
    compile 'cisd:jhdf5:14.12.6'  
    compile 'org.jfree:jfreechart:1.0.19'
    compile 'org.eclipse.jgit:org.eclipse.jgit:4.6.1.201703071140-r'
    compile 'org.slf4j:slf4j-nop:1.7.25'
    //compile 'org.zeromq:jeromq:0.5.1' #On BSREAD
    compile 'org.apache.commons:commons-math3:3.6.1'
    compile 'com.github.rjeschke:txtmark:0.13'
    //compile 'org.rxtx:rxtx:2.1.7'  
    compile group: 'com.neuronrobotics', name: 'nrjavaserial_java11', version: '3.15.0'    
    compile 'net.wimpi:jamod:1.2'
    compile 'net.java.dev.jna:jna:4.4.0'
    compile 'com.fifesoft:rsyntaxtextarea:2.6.1'
    compile 'jline:jline:2.14.3'
    compile 'ch.psi:bsread:4.0.0'
    compile 'net.imagej:ij:1.51j'    
    compile 'javax.mail:javax.mail-api:1.6.0'
    compile 'com.sun.mail:javax.mail:1.6.0'
    compile 'com.github.sarxos:webcam-capture:0.3.11'
    compile 'org.clojars.nakkaya:darcula:1.0.0'   //Netbeans darcula
    //compile 'com.bulenkov:darcula:2018.2'    //Original darcula  
    compile 'javax.xml.bind:jaxb-api:2.3.0'
    
    //TIFF support for ImageIO can be added installing JAI, or one of the following dependencies:
    //compile 'com.twelvemonkeys'.imageio:imageio-tiff:3.4.1'    
    //compile 'com.tomgibara.imageio:imageio-tiff:1.0'   //maven { url "http://www.tomgibara.com/maven/repo/" }     
    //compile 'com.github.jai-imageio:jai-imageio-core:1.4.0'
    //Or else adding to extensions folder:
    //https://github.com/jai-imageio/jai-imageio-core/releases/download/jai-imageio-core-1.4.0/jai-imageio-core-1.4.0.jar

    testCompile group: 'junit', name: 'junit', version: '4.10'
}

task sourcesJar(type: Jar, dependsOn: classes) {
classifier = 'sources'
from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
classifier = 'javadoc'
from javadoc.destinationDir
}

task fatJar(type: Jar, dependsOn: jar) {
    classifier 'fat'
    duplicatesStrategy = 'exclude'  
    manifest {
        attributes (
            'Main-Class': mainClass,
            'Implementation-Version': version,
            'Build-Time': timestamp,
            'Build-Type': type,
            'Add-Opens': addopens
            //,'Class-Path': configurations.compile.collect { it.getName() }.join(' ')
        )
    }    
    doFirst {
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } } 
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA', 'MANIFEST.MF', '*.INFO' , 'LICENSE'   
        
    with jar
}

artifacts {
    
    //archives sourcesJar
    //archives javadocJar
    archives fatJar
}

compileJava {
    //So we have parameter names for reflection
    options.compilerArgs << '-parameters'
}

uploadArchives {
    repositories {
        mavenDeployer {
            setUniqueVersion(true)
            repository(url: artifactoryUrlRel){
            	authentication(userName: artifactoryUser, password: artifactoryPwd)
            }
            snapshotRepository(url: artifactoryUrlRel){
            	authentication(userName: artifactoryUser, password: artifactoryPwd)
            }
            pom.groupId = project.group
            pom.artifactId =  project.name
            pom.version = version
        }
    }
}


jar {
    baseName = project.name
    from('src/main/assembly') {
        include '**/*'
        exclude '**/assembly.xml'
        exclude '**/script/**/*.class'
    }
    manifest {
        attributes (
            'Main-Class': mainClass, 
            'Implementation-Version': version,
            'Build-Time': timestamp,
            'Add-Opens': addopens
        )
    }       
}

task(debug, dependsOn: 'classes', type: JavaExec) {    
    //systemProperty 'java.awt.headless' ,true        
    systemProperty 'pshell.build.time', timestamp
    systemProperty 'pshell.build.type', type    
    main = mainClass
    classpath = sourceSets.main.runtimeClasspath
    args arguments
    ignoreExitValue = true
    debug true
}

task(run, dependsOn: 'classes', type: JavaExec) {      
    systemProperty 'pshell.build.time', timestamp
    systemProperty 'pshell.build.type', type
    main = mainClass
    classpath = sourceSets.main.runtimeClasspath
    args arguments 
    ignoreExitValue = true
    debug false
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId group
            artifactId project.name
            version version
        }
    }
}
    
bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']     
    pkg {
        repo = 'hltools'
        name = project.name
        userOrg = 'paulscherrerinstitute'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/paulscherrerinstitute/pshell'
        version {
            name = project.version
        }         
    }
}